@top QueryRequest { Query }

@top ConstraintListRequest { (Constraint (argsDelimiter Constraint)*)? }

@skip { space | Comment }

Query { "query" QueryBody }

QueryBody { ArgsOpening (RootConstraint (argsDelimiter RootConstraint)*) ArgsClosing }

/* todo temporary separation of constraint types before we have full LSP completion/linter */
RootConstraint { ("collection" | "filterBy" | "orderBy" | "require") ConstraintBody }

Constraint { constraintName ConstraintBody }

ConstraintBody { ArgsOpening (arg (argsDelimiter arg)*)? ArgsClosing }

arg {
    valueToken |
    Constraint
}

valueToken {
    PositionalParameter |
    NamedParameter |
    String |
    Int |
    Float |
    Boolean |
    Date |
    Time |
    DateTime |
    OffsetDateTime |
    Range {
        emptyRange { "[" "," "]" ~range } |
        dateTimeRange { "[" OffsetDateTime? "," OffsetDateTime? "]" ~range } |
        floatNumberRange { "[" Float? "," Float? "]" ~range } |
        intNumberRange { "[" Int? "," Int? "]" ~range }
    } |
    Uuid |
    Enum
}

@tokens {
    /* todo temporary list of supported constraint before we have full LSP completion/linter */
    constraintName {
        "filterGroupBy" |
        "and" |
        "or" |
        "not" |
        "userFilter" |
        "attributeEquals" |
        "attributeGreaterThan" |
        "attributeGreaterThanEquals" |
        "attributeLessThan" |
        "attributeLessThanEquals" |
        "attributeBetween" |
        "attributeInSet" |
        "attributeContains" |
        "attributeStartsWith" |
        "attributeEndsWith" |
        "attributeEqualsTrue" |
        "attributeEqualsFalse" |
        "attributeIs" |
        "attributeIsNull" |
        "attributeIsNotNull" |
        "attributeInRange" |
        "attributeInRangeNow" |
        "entityPrimaryKeyInSet" |
        "entityLocaleEquals" |
        "priceInCurrency" |
        "priceInPriceLists" |
        "priceValidInNow" |
        "priceValidIn" |
        "priceBetween" |
        "facetHaving" |
        "referenceHaving" |
        "hierarchyWithin" |
        "hierarchyWithinSelf" |
        "hierarchyWithinRoot" |
        "hierarchyWithinRootSelf" |
        "directRelation" |
        "having" |
        "excludingRoot" |
        "excluding" |
        "entityHaving" |
        "orderGroupBy" |
        "attributeNatural" |
        "attributeSetExact" |
        "attributeSetInFilter" |
        "priceNatural" |
        "random" |
        "referenceProperty" |
        "entityPrimaryKeyNatural" |
        "entityPrimaryKeyExact" |
        "entityPrimaryKeyInFilter" |
        "entityProperty" |
        "entityGroupProperty" |
        "page" |
        "strip" |
        "entityFetch" |
        "entityGroupFetch" |
        "attributeContent" |
        "attributeContentAll" |
        "priceContent" |
        "priceContentAll" |
        "priceContentRespectingFilter" |
        "associatedDataContent" |
        "associatedDataContentAll" |
        "referenceContentAll" |
        "referenceContent" |
        "referenceContentAllWithAttributes" |
        "referenceContentWithAttributes" |
        "hierarchyContent" |
        "priceType" |
        "dataInLocalesAll" |
        "dataInLocales" |
        "facetSummary" |
        "facetSummaryOfReference" |
        "facetGroupsConjunction" |
        "facetGroupsDisjunction" |
        "facetGroupsNegation" |
        "attributeHistogram" |
        "priceHistogram" |
        "distance" |
        "level" |
        "node" |
        "stopAt" |
        "statistics" |
        "fromRoot" |
        "fromNode" |
        "children" |
        "siblings" |
        "parents" |
        "hierarchyOfSelf" |
        "hierarchyOfReference" |
        "queryTelemetry"
    }

    PositionalParameter { "?" }

    NamedParameter { "@" @asciiLowercase $[a-zA-Z0-9]*  }

    String {
        ('"' char* '"') |
        /* todo not perfect, doesn't support " in between '' without escape character */
        ('\'' char* '\'')
    }
    char { $[\u{20}\u{21}\u{23}-\u{5b}\u{5d}-\u{10ffff}] | "\\" esc }
    esc  { $["\\\/bfnrt] | "u" hex hex hex hex }
    hex  { $[0-9a-fA-F] }

    Int { "-"? @digit+ }

    Float { "-"? @digit+ "." @digit+ }

    Boolean { "true" | "false" }

    Date { @digit @digit @digit @digit "-" @digit @digit "-" @digit @digit }

    Time { @digit @digit ":" @digit @digit ":" @digit @digit ("." @digit+)? }

    DateTime { Date "T" Time }

    OffsetDateTime { DateTime ("+" | "-") @digit @digit ":" @digit @digit }

    Uuid { $[a-z0-9]+ "-" $[a-z0-9]+ "-" $[a-z0-9]+ "-" $[a-z0-9]+ "-" $[a-z0-9]+ }

    Enum { @asciiUppercase+ ("_" @asciiUppercase+)* }

    argsDelimiter { "," }

    ArgsOpening { "(" }

    ArgsClosing { ")" }

    Comment { "//" ![\n]* }

    space { $[ \t\n\r]+ }

    @precedence {
        constraintName,
        String,
        OffsetDateTime,
        DateTime,
        Date,
        Time,
        Float,
        Int,
        Boolean,
        Uuid,
        Enum,
        NamedParameter,
        PositionalParameter
    }
}

@detectDelim